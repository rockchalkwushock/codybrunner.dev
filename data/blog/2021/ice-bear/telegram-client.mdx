---
archived: false
createdAt: 07/07/2021
description: "Part 3: Writing my own Telegram client to better understand underlying API's in Elixir and how these applications get used in the greater context of a Phoenix application."
keywords:
  - elixir
  - phoenix
  - phoenix liveview
  - telegram
  - telegram bot
  - telegram client
published: true
tags:
  - elixir
  - live-view
  - phoenix
title: 'Ice Bear: Telegram Client'
---

import { Signature } from '../components/MDXComponents.tsx'

> Unfortunately I got pretty busy with job interviews and married life stuff so I haven't had much time in the last few weeks to work on IceBear. I have written the functionality for the Telegram client and the polling feature. I am trying to get this post and one more out the door before my wife and I travel and then move to another home...so here goes nothing! ðŸ˜«ðŸ˜°

As I stated in the introduction post this is the first Elixir/Phoenix application that I have built that is not just following along with a tutorial. I could easily use one of the many clients present on [hex.pm](https://hex.pm/) for creating a Telegram bot. The whole point of this project is not just to create something that will help me an my wife keep track of our task better, but to really gain a better understanding of the core technologies being used. So I am reinventing the wheel, and yes it comes at the cost of speed, but with the pay off of knowledge which is ultimately worth the tradeoff.

## Requirements for my Telegram Client

1. Must be able to make authenticated requests to the [Bot API](https://core.telegram.org/bots) as Ice Bear.
2. Must be able to query the bot.
3. Must be able to get updates.
4. Must be able to send messages.

There is quite a bit more to the Telegram Bot API, but at this point in time I don't see anything else my wife and I would need to achieve the MVP of this project. So unlike the other Telegram Clients available for me to use this will be a pretty stripped down client with minimal functionality, and I'm okay with that. At any point in the future I will easily be able to extend the internal client to add functionality or after feeling confident with the underlying APIs of Elixir I can opt to replace the custom code with one of the Telegram clients.

Before I get started writing the client let's take a look at the Telegram Bot API and see what I need. All requests will look similar to the following:

```shell
# https://api.telegram.org/bot<token>/METHOD_NAME
# https://api.telegram.org/botabc:123/getMe
```

This tells me I need to have the base uri for the Telegram API as a constant in the client application so I can easily reach for it and cut down on having that string everywhere. Elixir has a nice way of creating constants inside of modules using what is known as a [module attribute](https://elixir-lang.org/getting-started/module-attributes.html):

```elixir
defmodule IceBear.Telegram do
  # ...
  @api_base_uri "https://api.telegram.org"
  # ...
end
```

This is great because when I later want to assemble the full address for the request I can use one of two methods for stitching the request uri together.

```elixir
defmodule IceBear.Telegram do
  # ...
  @api_base_uri "https://api.telegram.org"
  # ...
  # Concatenation Operator: @api_base_uri <> token <> "/bot" <> method
  # String interpolation: "#{@api_base_uri}#{token}/bot#{method}"
end
```

Telegram has a lot of endpoints with a multitude of functionality, but these are what I will be using:

- [/getMe](https://core.telegram.org/bots/api#getme)
- [/getUpdates](https://core.telegram.org/bots/api#getupdates)
- [/sendMessage](https://core.telegram.org/bots/api#sendmessage)

## Finding an HTTP Client

There are quite a few HTTP clients available on [hex.pm](https://hex.pm/). I read about options for an HTTP client in this [article](https://blog.appsignal.com/2020/07/28/the-state-of-elixir-http-clients.html) from [AppSignal](https://www.appsignal.com/). I really wanted to go for something native instead of reaching for a third-party library; however after reading the article the native [httpc](http://erlang.org/doc/man/httpc.html) module that is part of the BEAM didn't feel like a good choice with its limitations. The article sold me on giving the library, [Finch](https://hexdocs.pm/finch/Finch.html) a try over the popular [HTTPoison](https://hexdocs.pm/httpoison/readme.html) and [Tesla](https://hexdocs.pm/tesla/readme.html).

## Working with Finch ðŸ¦

First off I need to add `finch` to my `mix.exs` and install it as a dependency to Ice Bear.

```diff:mix.exs
defmodule IceBear.MixProject do
  ...
  defp deps do
    [
      {:phoenix, "~> 1.5.8"},
      {:phoenix_ecto, "~> 4.1"},
      {:ecto_sql, "~> 3.4"},
      {:postgrex, ">= 0.0.0"},
      {:phoenix_live_view, "~> 0.15.1"},
      {:floki, ">= 0.27.0", only: :test},
      {:phoenix_html, "~> 2.11"},
      {:phoenix_live_reload, "~> 1.2", only: :dev},
      {:phoenix_live_dashboard, "~> 0.4"},
      {:telemetry_metrics, "~> 0.4"},
      {:telemetry_poller, "~> 0.4"},
      {:gettext, "~> 0.11"},
      {:jason, "~> 1.0"},
      {:plug_cowboy, "~> 2.0"},
      {:credo, "~> 1.5", only: [:dev, :test], runtime: false},
-     {:dialyxir, "~> 1.1", only: [:dev, :test], runtime: false}
+     {:dialyxir, "~> 1.1", only: [:dev, :test], runtime: false},
+     {:finch, "~> 0.7.0"}
    ]
  end
  ...
end
```

```shell
mix deps.get
```

The next thing I will do is to create a sub directory of `/lib/ice_bear` that will encompass the context of my Telegram client code. I will start off with just creating the `client.ex`.

```shell
mkdir lib/ice_bear/telegram
touch lib/ice_bear/telegram/client.ex
```

Finch itself operates as a Supervisor which I will touch on in a later post. I need to get Finch spun up and accepting requests by initializing the connection pool and then adding this process to the application tree for the Phoenix application. That was a mouthful and what the hell did it actually mean. When Phoenix starts up it looks at the `application.ex` file and goes through all the processes it needs to start up before it is ready to start serving up requests to the end user. Looking at this file we can see that Phoenix starts up the connection to the database, a telemetry tool, the PubSub module for realtime communication, and the Endpoint for the web server. I will need to add my Finch connection pool in there so Phoenix knows it needs to start up that process before Ice Bear can start doing his thing.

```elixir:lib/ice_bear/telegram/client.ex
defmodule IceBear.Telegram.Client do
  require Logger

  @api_base_uri "https://api.telegram.org"
  @pool_size 2

  def child_spec do
    Logger.debug("----> Finch Connection Pool is being established.")

    pool =
      {Finch,
       name: __MODULE__, # IceBear.Telegram.Client
       pools: %{
         @api_base_uri => [size: @pool_size]
       }}

    Logger.debug("----> Finch Connection Pool established: #{inspect(pool)}")

    pool
  end
end
```

```diff:lib/ice_bear/application.ex
defmodule IceBear.Application do
  ...
  def start(_type, _args) do
    children = [
      # Start the Ecto repository
      IceBear.Repo,
      # Start the Telemetry supervisor
      IceBearWeb.Telemetry,
      # Start the PubSub system
      {Phoenix.PubSub, name: IceBear.PubSub},
      # Start the Endpoint (http/https)
-     IceBearWeb.Endpoint
+     IceBearWeb.Endpoint,
+     # Start Finch Connection Pool.
+     IceBear.Telegram.Client.child_spec()
    ]

    # See https://hexdocs.pm/elixir/Supervisor.html
    # for other strategies and supported options
    opts = [strategy: :one_for_one, name: IceBear.Supervisor]
    Supervisor.start_link(children, opts)
  end
  ...
end
```

Now we can slide on over to `iex` and check to see if this code is running as I would expect it too:

```shell
iex -S mix                                                                                       at 07:00:46
Erlang/OTP 24 [erts-12.0.3] [source] [64-bit] [smp:16:16] [ds:16:16:10] [async-threads:1] [jit] [dtrace]

[debug] ----> Finch Connection Pool is being established.
[debug] ----> Finch Connection Pool established: {Finch, [name: IceBear.Telegram.Client, pools: %{"https://api.telegram.org" => [size: 2]}]}
Interactive Elixir (1.12.2) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)>
```

You can see that we get the debug logs and that the connection pool has been created and the process is being executed in the context of the application tree by Phoenix.

## Fetching information about Ice Bear

There are two things I need to achieve initially and they work in tandem to each other.

1. I need to make sure my `bot_token` that I received works to authenticate me with the Telegram Bot API.
2. I need to make sure I can make a request to get information about Ice Bear using the `getMe` endpoint.

Finch has a function for building out the request URL in `build/4` and I will use `request/3` to actually send the HTTP request. The code ends up looking really beautiful with the pipe operator. ðŸ˜

#### A quick aside on the pipe operator

The pipe operator allows me to chain function calls together where the return of the function is the input of the next. The pipe operator already understands that the first argument of the next function is the return value of the previous so there is no need to specify it.

```elixir:example/pipe_operator.ex
defmodule MyApp.Example do
  def do_math(value, value_to_add, value_to_subtract) do
    value
    |> add(value_to_add) # value + value_to_add
    |> double() # return * 2
    |> minus(value_to_subtract) # return - value_to_subtract
  end

  defp add(n, v), do: n + v
  defp double(n), do: n * 2
  defp minus(n, v), do: n - v
end

# iex> MyApp.Example.do_math(100, 15, 22) --> 208
# 100 + 15 = 115
# 115 * 2 = 230
# 230 - 22 = 208
```

```elixir:lib/ice_bear/telegram/client.ex
defmodule IceBear.Telegram.Client do
  alias Finch.Response
  require Logger

  @api_base_uri "https://api.telegram.org"
  @default_headers [
    {"Accept", "application/json"},
    {"Content-Type", "application/json"}
  ]

  # ...

  def get_me do
    Logger.debug("----> IceBear.Telegram.Client.get_me/0")

    :get
    |> Finch.build("#{@api_base_uri}/bot#{fetch_token()}/getMe", @default_headers, nil)
    |> Finch.request(__MODULE__)
    |> parse_response()
    |> process_response()
  end

  # I will use this private function on the module to inject my bot_token so that every
  # request I send to the Telegram Bot API is 1) built correctly and 2) authenticated.
  defp fetch_token, do: Application.fetch_env!(:ice_bear, :bot_token)

  # parse_response/1 will take the Finch.Response and parse the 'body' to json.
  defp parse_response({:ok, %Response{body: body}}), do: body |> Jason.decode!()

  # or it will return the error response.
  defp parse_response({:error, response}), do: response

  # process_response/1 will take return the result of the query.
  defp process_response(%{"ok" => true, "result" => result}), do: {:ok, result}

  # or it will return the reason the query failed.
  defp process_response(%Mint.TransportError{reason: reason}), do: reason
end
```

Now I can test this out from the terminal and see if I can make a successful query to Telegram for IceBear's bot information.

```shell
iex -S mix                                                                                          at 07:02:47
Erlang/OTP 24 [erts-12.0.3] [source] [64-bit] [smp:16:16] [ds:16:16:10] [async-threads:1] [jit] [dtrace]

Compiling 1 file (.ex)
[debug] ----> Finch Connection Pool is being established.
[debug] ----> Finch Connection Pool established: {Finch, [name: IceBear.Telegram.Client, pools: %{"https://api.telegram.org" => [size: 2]}]}
Interactive Elixir (1.12.2) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)> alias IceBear.Telegram.Client
IceBear.Telegram.Client
iex(2)> Client.get_me
[debug] ----> IceBear.Telegram.Client.get_me/0
{:ok,
 %{
   "can_join_groups" => false,
   "can_read_all_group_messages" => false,
   "first_name" => "IceBearBot",
   "id" => 1895435207,
   "is_bot" => true,
   "supports_inline_queries" => false,
   "username" => "icebarebot"
 }}
iex(3)>
```

Great! Now the rest of this should not be too difficult from here. I only have two other endpoints that I need to write for the functionality I am desiring.

## Ice Bear gets updates

```elixir:lib/ice_bear/telegram/client.ex
defmodule IceBear.Telegram.Client do
  # ...

  def get_updates(last_seen) do
    Logger.debug("----> IceBear.Telegram.Client.get_updates/1")

    :get
    |> Finch.build(
      "#{@api_base_uri}/bot#{fetch_token()}/getUpdates",
      @default_headers,
      Jason.encode!(%{
        "offset" => last_seen + 1,
        "timeout" => 30
      })
    )
    |> Finch.request(__MODULE__)
    |> parse_response()
    |> process_response()
  end

  # ...
end
```

```shell
iex -S mix                                                                                          at 09:33:15
Erlang/OTP 24 [erts-12.0.3] [source] [64-bit] [smp:16:16] [ds:16:16:10] [async-threads:1] [jit] [dtrace]

Compiling 1 file (.ex)
[debug] ----> Finch Connection Pool is being established.
[debug] ----> Finch Connection Pool established: {Finch, [name: IceBear.Telegram.Client, pools: %{"https://api.telegram.org" => [size: 2]}]}
Interactive Elixir (1.12.2) - press Ctrl+C to exit (type h() ENTER for help)
iex(1)> alias IceBear.Telegram.Client
IceBear.Telegram.Client
iex(2)> Client.get_updates(1)
[debug] ----> IceBear.Telegram.Client.get_updates/1
{:ok,
 [
   %{
     "message" => %{
       "chat" => %{
         "first_name" => "Cody",
         "id" => 248725869,
         "last_name" => "Brunner",
         "type" => "private",
         "username" => "rockchalkwushock"
       },
       "date" => 1625624031,
       "from" => %{
         "first_name" => "Cody",
         "id" => 248725869,
         "is_bot" => false,
         "language_code" => "en",
         "last_name" => "Brunner",
         "username" => "rockchalkwushock"
       },
       "message_id" => 43,
       "text" => "Helo"
     },
     "update_id" => 160025528
   }
 ]}
iex(3)>
```

Now that Ice Bear can check for updates later on I can write code that will poll the Telegram Bot API using this endpoint to check and see when my wife or myself has interacted with Ice Bear. Using that information I will then be able to execute the appropriate action based on the interaction.

## Ice Bear sends message

On to what will be the bread and butter of Ice Bear, sending messages!

```elixir:lib/ice_bear/telegram/client.ex
defmodule IceBear.Telegram.Client do
  # ...

  def send_message(chat_id, text) do
    Logger.debug("----> IceBear.Telegram.Client.send_message/2")

    :post
    |> Finch.build(
      "#{@api_base_uri}/bot#{fetch_token()}/sendMessage",
      @default_headers,
      Jason.encode!(%{
        "chat_id" => chat_id,
        "text" => text
      })
    )
    |> Finch.request(__MODULE__)
    |> parse_response()
    |> process_response()
  end

  # ...
end
```

In the same iex shell I can grab the `chat["id"]` and add it along with a message for Ice Bear to send to myself and I can test out that the endpoint is actually working I expect it too.

```shell
# ...
iex(3)> Client.send_message(248725869, "Hello World")
[debug] ----> IceBear.Telegram.Client.send_message/2
{:ok,
 %{
   "chat" => %{
     "first_name" => "Cody",
     "id" => 248725869,
     "last_name" => "Brunner",
     "type" => "private",
     "username" => "rockchalkwushock"
   },
   "date" => 1625668482,
   "from" => %{
     "first_name" => "IceBearBot",
     "id" => 1895435207,
     "is_bot" => true,
     "username" => "icebarebot"
   },
   "message_id" => 44,
   "text" => "Hello World"
 }}
iex(4)>
```

And not only do I get the output in iex that the request was a success I just heard the Telegram notification sound in my AirPods. If it's my wife or someone else and not Ice Bear I'm gonna be pissed!

![Ice Bear sends his first message](/images/2021/ice-bear/ice-bear-first-message.png)

ðŸŽ‰ðŸŽ‰ **ICE BEAR LIVES!!!** ðŸŽ‰ðŸŽ‰

That pretty much wraps up the desired functionality for the Telegram client at the moment. I still have a bit of a problem though. How does IceBear go about polling the Telegram Bot API for messages from me and my wife? Obviously I don't want to have to manually call `get_updates/1` to see if something has changed. This is where I will lean on the OTP features of Elixir and the BEAM. In the next post I will write the code that will poll the Telegram Bot API so IceBear knows what is going on. I will get into working with two new native modules to Elixir: `GenServer` and `Supervisor`. Stay tuned to see how IceBear is coming along!

You can find the code that pertains to this post in the below PR:

[Pull Request #7: Custom Telegram Client](https://github.com/rockchalkwushock/ice-bear/pull/7)

<Signature>~ Cody ðŸš€</Signature>

- [Introduction](https://codybrunner.dev/blog/2021/ice-bear/introduction)
- [Setting Up Ice Bear](https://codybrunner.dev/blog/2021/ice-bear/setting-up-ice-bear)
